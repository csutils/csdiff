# Copyright (C) 2011 - 2020 Red Hat, Inc.
#
# This file is part of csdiff.
#
# csdiff is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# any later version.
#
# csdiff is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with csdiff.  If not, see <http://www.gnu.org/licenses/>.

# common setup for all tests
set(csdiff      "${CMAKE_BINARY_DIR}/src/csdiff")
set(csgrep      "${CMAKE_BINARY_DIR}/src/csgrep")
set(cssort      "${CMAKE_BINARY_DIR}/src/cssort")
set(testdir     "${CMAKE_CURRENT_SOURCE_DIR}")
set(csjson      "${csgrep} --mode=json")
set(diffcmd     "diff -up")

# unify the JSON output generated by different version of Boost
set(jsfilter "sed")
set(jsfilter "${jsfilter} -e ':a;N;$!ba;s|:\\\\\\\\n *\\\\\\\\[|: [|g'")

macro(add_test_wrap test_name cmd)
    add_test("${test_name}" bash -c "${cmd}")
    set_tests_properties(${test_name} PROPERTIES COST ${test_cost})
    math(EXPR test_cost "${test_cost} - 1")
endmacro()

set(test_cost 1048576)

add_subdirectory(csdiff)
add_subdirectory(csgrep)
add_subdirectory(cssort)

# a generic template for cstrans-df-run tests
macro(tests_cstrans_df_run tst)
    set(test_data_prefix "${testdir}/cstrans-df-run/${tst}")
    set(cmd "${CMAKE_BINARY_DIR}/src/cstrans-df-run")
    set(cmd "${cmd} -- /opt/cov-sa-2019.09/bin/cov-build --dir=/cov --append-log")
    set(cmd "${cmd} <${test_data_prefix}-stdin.txt")
    set(cmd "${cmd} | ${diffcmd} ${test_data_prefix}-stdout.txt -")
    add_test_wrap("cstrans-df-run-${tst}" "${cmd}")
endmacro()

# cstrans-df-run tests
tests_cstrans_df_run(0001)
tests_cstrans_df_run(0002)
tests_cstrans_df_run(0003)
tests_cstrans_df_run(0004)
tests_cstrans_df_run(0005)
tests_cstrans_df_run(0006)
tests_cstrans_df_run(0007)
tests_cstrans_df_run(0008)
tests_cstrans_df_run(0009)
